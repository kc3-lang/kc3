## kc3
## Copyright from 2022 to 2025 kmx.io <contact@kmx.io>
##
## Permission is hereby granted to use this software granted the above
## copyright notice and this permission paragraph are included in all
## copies and substantial portions of this software.
##
## THIS SOFTWARE IS PROVIDED "AS-IS" WITHOUT ANY GUARANTEE OF
## PURPOSE AND PERFORMANCE. IN NO EVENT WHATSOEVER SHALL THE
## AUTHOR BE CONSIDERED LIABLE FOR THE USE AND PERFORMANCE OF
## THIS SOFTWARE.

defmodule List do

  def append = cfn List "list_init_append" (Result, List, Tag)

  def cast = cfn List "list_init_cast" (Result, Sym, Tag)

  def count = fn {
    (n) { count(n, []) }
    (0, acc) { acc }
    (n, acc) {
      if n > 0 do
        count(n - 1, [n | acc])
      else
        []
      end
    }
  }

  def count_items = fn {
    (list) { count_items(list, %{}) }
    ([], map) { map }
    ([first | rest], map) {
      count = if map[first] do map[first] else 0 end
      map = Map.put(map, first, count + 1)
      count_items(rest, map)
    }
  }

  def each = cfn Bool "list_each" (List, Callable, Result)

  def filter = cfn List "list_filter" (List, Callable, Result)

  def find_if = fn {
    ([], _) { false }
    ([a | b], f) do
      c = f(a)
      if c do
        c
      else
        find_if(b, f)
      end              
    end
  }

  def first = fn ([a | _]) { a }

  def has? = cfn Bool "list_has" (List, Tag, Result)

  def join = fn {
    (list, separator) { join(list, separator, []) }
    ([], sep, acc) { str(reverse(acc)) }
    ([first, second | rest], sep, acc) {
      join([second | rest], sep, [sep, first | acc])
    }
    ([first], sep, acc) { str(reverse([first | acc])) }
  }

  def last = cfn Tag "list_last" (List, Result)

  def length = cfn Tag "kc3_list_length" (List, Result)

  def map = cfn List "list_map" (List, Callable, Result)

  def next = fn ([a | b]) { b }

  def reverse = fn {
    (x) { reverse(x, []) }
    ([], acc) { acc }
    ([a | b], acc) { reverse(b, [a | acc]) }
  }

  def slice = cfn List "list_slice" (List, Tag, Tag, Result)

  def sort = cfn List "list_sort" (List, Result)

  def sort_by = cfn List "list_sort_by" (List, Callable, Result)

  def to_array = cfn Array "kc3_list_to_array" (List, Sym, Result)

  def unique = cfn List "list_unique" (List, Result)

end
