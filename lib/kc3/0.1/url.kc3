## kc3
## Copyright from 2022 to 2026 kmx.io <contact@kmx.io>
##
## Permission is hereby granted to use this software granted the above
## copyright notice and this permission paragraph are included in all
## copies and substantial portions of this software.
##
## THIS SOFTWARE IS PROVIDED "AS-IS" WITHOUT ANY GUARANTEE OF
## PURPOSE AND PERFORMANCE. IN NO EVENT WHATSOEVER SHALL THE
## AUTHOR BE CONSIDERED LIABLE FOR THE USE AND PERFORMANCE OF
## THIS SOFTWARE.

dlopen("http.so")

defmodule URL do

  def decode_query_string = fn (url) {
    pos = Str.position_str(url, "?")
    q = if pos >= 0 do
      Str.slice(url, pos + 1, -1)
    else
      ""
    end
    parts = Str.split(q, "&")
    map = %{}
    while parts != [] do
      [part | parts] = ^ parts
      pos = Str.position_str(part, "=")
      if pos >= 0 do
        key = Str.slice(part, 0, pos)
        value = Str.slice(part, pos + 1, -1)
        map = Map.put(map, key, value)
      end
    end
    map
  }

  def eat = fn {
    ([], _) { "/" }
    (url, 0) {
      if (type(url) == List) do
        "/" + List.join(url, "/")
      else
        url
      end
    }
    ([first | rest], i) { eat(rest, i - 1) }
    (url, i) { eat(Str.split(url, "/"), i + 1) }
  }

  def escape = cfn Str "url_escape" (Str, Result)

  def escapes = "%+#<>'\\\""

  def encode_form = fn (map) {
    List.join(Map.map_to_list(map, fn (key, value) {
      "#{URL.escape(key)}=#{URL.escape(value)}" }), "&")
  }

  def unescape = cfn Str "url_unescape" (Str, Result)

end
