defmodule HTTPd do

  def server_loop = fn (client) {
    puts("HTTPd.server_loop: got client #{client}")
    ok = true
    while ok do
      req = HTTP.Request.buf_parse(client.buf_rw.r)
      res = %HTTP.Response{body: "
<html>
  <head>
    <title>KC3 HTTPd</title>
  </head>
  <body>
    <h1>KC3 HTTPd</h1>
    <p>
      Hello from #{__FILE__}
    </p>
    <h2>Request</h2>
    <pre><code>#{req}</code></pre>
  </body>
</html>
"}
      if (HTTP.Response.buf_write(res, client.buf_rw.w) < 0) do
        ok = false
      end
    end
  }

  def server = fn (host, port) {
    socket = Socket.listen(host, port)
    puts("KC3 HTTPd: listening on #{host}:#{port}")
    while true do
      client = Socket.Buf.accept(socket)
      server_loop(client)
    end
  }

  def main = fn {
    () {
      host = getenv("KC3_HTTPD_HOST")
      port = getenv("KC3_HTTPD_PORT")
      server(host, port)
    }
    (host, port) {
      server(host, port)
    }     
  }

end
